plugins {
    id 'org.springframework.boot' version '2.4.1'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'com.moowork.node' version '1.3.1'
    id 'org.hidetake.ssh' version '2.10.1'
}

group = 'net.catenoid.ticketlink'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.apache.commons:commons-lang3'
    implementation 'commons-codec:commons-codec'
}

test {
    useJUnitPlatform()
}

project.ext {
    jarBaseName = 'app'
    jarVersion = '0.0.1-' + new Date().format('yyyyMMdd_HHmmss')
}

node {
    version='14.15.3'
    npmVersion='6.14.9'
    workDir = file("./src/frontend")
    npmWorkDir = file("./src/frontend")
    nodeModulesDir = file("./src/frontend")
}

task setUp(type:NpmTask) {
    description = "Install Node.js packages"
    args=['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task buildFrontEnd(type:NpmTask, dependsOn: setUp){
    description = "Build Front"
    args = ['run', 'build']
}

processResources.dependsOn 'buildFrontEnd'

remotes {
    webServer {
        host = 'hyeond-pc.asuscomm.com'
        user = 'hyeond'
        password = 'blind3256@!'
        port = 2222
        knownHosts = allowAnyHosts
    }
}

bootJar {
    archiveBaseName = project.jarBaseName
    archiveVersion = project.jarVersion
}

task deploy {
    doLast {
        ssh.run {
            session(remotes.webServer) {
                final jarName = project.jarBaseName + "-" + project.jarVersion + ".jar"
                put from: "build/libs/${jarName}", into: '/home/hyeond/GoHomeDaddy/ticketlink'
                execute "ps -ef | grep app- | grep -v grep | awk '{print \"kill -9\",\$2}' | sh -v"
                execute "nohup java -jar /home/hyeond/GoHomeDaddy/ticketlink/${jarName} < /dev/null > /dev/null  2> /dev/null &"
//                executeBackground "nohup java -jar /home/hyeond/GoHomeDaddy/ticketlink/${jarName} &"
            }
        }
    }
}
